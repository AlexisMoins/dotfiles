# Author: Alexis Moins
# Creation: 18 juin 2020
# vim: ft=zsh syn=zsh

# Enable variable / function substitution in the prompt
setopt PROMPTSUBST

# Load the version control function
autoload vcs_info

# Execute the version control function before each prompt
precmd_functions+=( vcs_info )

# Styles used for both regular and special actions
zstyle ':vcs_info:*' formats '%F{5} on â†‘ %b%u%c%f'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' actionformats '%F{5} on %b %F{3}(%a)%f%u%c%f'

# Style used for staged and unstaged files
zstyle ':vcs_info:*' stagedstr ' %F{1}[&]%f'
zstyle ':vcs_info:*' unstagedstr ' %F{1}[!]%f'

# Get the current python virtual env
get-virtualenv() {
    # If we are in a virtal environment
    if [ -n "${VIRTUAL_ENV}" ]; then
        # Fetch the virtual env name
        python_env="$(basename "${VIRTUAL_ENV}")"

        # Create the prompt
        venv_prompt="%F{3} using ${python_env}%f "

        # Print the value of ${venv_prompt} to standard out.
        printf "%s" "${venv_prompt}"
    fi
}

# Single quotes are mandatory for vcs_info to be interpreted correctly
PROMPT='
%(1j.:%j .)%(#.%F{1}root %f.)%F{6}%1~%f${vcs_info_msg_0_}$(get-virtualenv)
%(?..%F{1})->%f '

# Prompt pour le corps des fonctions
PROMPT2="%F{2}... %f"

# Prompt used for command correction
SPROMPT='zsh: correct %F{1}%R%f to %F{10}%r%f [nyae] ? '
